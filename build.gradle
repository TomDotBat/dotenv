plugins {
    id "java-library"
    id "maven-publish"
}

group "net.forgium"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.0"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/forgium/dotenv")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = "net.forgium"
            artifactId = "dotenv"
            version = "1.0.0"

            from(components.java)
        }
    }
}

tasks.withType(PublishToMavenRepository) { it.dependsOn test }